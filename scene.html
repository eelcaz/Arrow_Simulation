<!DOCTYPE html>
<html>

<script id="arrow-vertex-shader" type="x-shader/x-vertex">
    attribute vec4 vPosition;
    attribute vec2 vTexCoord;
    attribute vec3 vNorm;

    uniform vec3 light1WorldPosition;
    uniform vec3 light2WorldPosition;

    uniform mat4 rotateMatrix;
    uniform mat4 translateMatrix;
    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;

    varying vec2 fTexCoord;

    varying vec3 fNorm;

    // light 1 is static point, light 2 and view are camera point--view included for code comprehension
    varying vec3 fSurfaceToLight1;
    varying vec3 fSurfaceToLight2;
    varying vec3 fSurfaceToView;

    void main()
    {
        gl_Position =  projectionMatrix*modelViewMatrix*translateMatrix*rotateMatrix*vPosition;

        vec3 surfaceWorldPosition = (modelViewMatrix * translateMatrix*rotateMatrix * vPosition).xyz;

        fSurfaceToLight1 = mat3(modelViewMatrix)*light1WorldPosition - surfaceWorldPosition;
        fSurfaceToLight2 = light2WorldPosition - surfaceWorldPosition;

        fSurfaceToView = fSurfaceToLight2;

        fTexCoord = vTexCoord;
        fNorm = mat3(modelViewMatrix * translateMatrix * rotateMatrix)*vNorm;
    }
</script>

<script id="arrow-fragment-shader" type="x-shader/x-fragment">
    precision mediump float;
    
    uniform sampler2D texture;
    varying vec2 fTexCoord;
    
    varying vec3 fNorm;
    
    varying vec3 fSurfaceToLight1;
    varying vec3 fSurfaceToLight2;
    varying vec3 fSurfaceToView;
    
    void main()
    {
        float L1 = 0.5;
        float L2 = 0.8;
    
        vec4 ambient = vec4(0.05, 0.05, 0.05, 1);
        // vec3 kd = vec3(0.4, 0.4, 0.4);
        vec4 ks = vec4(0.5, 0.5, 0.5, 1);
    
        vec3 normal = normalize(fNorm);
        
        vec3 surfaceToLight1Direction = normalize(fSurfaceToLight1);
        vec3 surfaceToViewDirection = normalize(fSurfaceToView);
        vec3 halfVector1 = normalize(surfaceToLight1Direction + surfaceToViewDirection);
    
        vec3 surfaceToLight2Direction = normalize(fSurfaceToLight2);
        vec3 halfVector2 = normalize(surfaceToLight2Direction + surfaceToViewDirection);
        
        float light1 = dot(normal, surfaceToLight1Direction);
        float specular1 = max(dot(normal, halfVector1), 0.0);
        float dullness = 200.0;
        if(specular1 > 0.0) 
            specular1 = pow(specular1, dullness);
    
        float light2 = dot(normal, surfaceToLight2Direction);
        float specular2 = max(dot(normal, halfVector2), 0.0);
        if(specular2 > 0.0)
            specular2 = pow(specular2, dullness); 
    
        vec4 kd = texture2D(texture, fTexCoord);
    
        gl_FragColor = L1 * ambient + L2 * ambient;

        gl_FragColor += kd*max(light1, 0.0)*L1 + kd*max(light2, 0.0)*L2;
    
        gl_FragColor += ks*specular1*L1 + ks*specular2*L2;
        
        // gl_FragColor += ambient;     
    }
</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="objLoader.js"></script>
<script type="text/javascript" src="scene.js"></script>

<body>

    <canvas id="gl-canvas" width="512" height="512">
        Whoopsies, you don't have HTML5 canvas support on this browser!
    </canvas>
    <img id="arrow_image" src="download.png" hidden></img>

</body>

</html>